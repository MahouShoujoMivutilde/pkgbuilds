pkgname=dmenu-mivu
_pkgname=dmenu
pkgver=4.9
pkgrel=1
pkgdesc='Generic menu for X'
url='https://tools.suckless.org/dmenu/'
arch=('x86_64')
license=('MIT')
depends=('sh' 'libxinerama' 'libxft' 'freetype2')

source=("https://dl.suckless.org/tools/${_pkgname}-${pkgver}.tar.gz"
        'colors.diff'
        'dmenu-lineheight-4.9.diff')
sha256sums=('b3971f4f354476a37b2afb498693649009b201550b0c7c88e866af8132b64945'
            '4c355d34fe44ae87e63964b1db5ddb144d1040d771c32650e93e52ac7b1fe1c8'
            '18e52acc2eabc6aaa8b65c0ac96228ae63a97d2525fc0902882a1e423a6a5a0b')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

prepare() {
  cp *.diff ${_pkgname}-${pkgver}
  cd ${_pkgname}-${pkgver}
  echo "CPPFLAGS+=${CPPFLAGS}" >> config.mk
  echo "CFLAGS+=${CFLAGS}" >> config.mk
  echo "LDFLAGS+=${LDFLAGS}" >> config.mk
  patch -p1 < colors.diff
  patch -p1 < dmenu-lineheight-4.9.diff
}

build() {
  cd ${_pkgname}-${pkgver}
  make \
      X11INC=/usr/include/X11 \
      X11LIB=/usr/lib/X11 \
      FREETYPEINC=/usr/include/freetype2
}

package() {
  cd ${_pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgname}"
}

# vim: ts=2 sw=2 et:
