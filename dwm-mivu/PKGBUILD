pkgname=dwm-mivu-git
_pkgname=dwm
pkgver=6.2.r1705.71b7353
pkgrel=1
pkgdesc='A heavily patched dwm fork'
url='https://github.com/MahouShoujoMivutilde/dwm'
arch=('i686' 'x86_64')
license=('MIT')
options=('zipman')
depends=('libx11' 'libxinerama' 'libxft' 'freetype2')
makedepends=('git')
source=("$pkgname::git+https://github.com/MahouShoujoMivutilde/dwm.git#branch=all")
sha256sums=(SKIP)

provides=("$_pkgname")
conflicts=("$_pkgname")

pkgver() {
    cd "$srcdir/$pkgname"
    printf '%s.r%s.%s\n' "$(awk '/^VERSION =/ {print $3}' config.mk)" \
        "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/$pkgname"
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
    cd "$srcdir/$pkgname"
    make PREFIX=/usr DESTDIR="$pkgdir" install
    install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
    install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
}
